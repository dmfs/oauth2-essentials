apply plugin: 'java-library'

sourceSets {
    main {
        java {
            srcDirs 'src/main/java'
            srcDirs 'build/generated/java'
        }
    }
}

def gitVersion = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'describe', '--tags'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

group 'org.dmfs'
version gitVersion()

tasks.withType(Jar) {
    eachFile {
        it.duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    }
}

sourceCompatibility = 1.8

configurations {
    pom
}

apply from: 'publish.gradle'

repositories {
    mavenCentral()
}

import org.apache.tools.ant.filters.ReplaceTokens

task generateSources(type: Copy) {
    from 'src/templates/java'
    into "${buildDir}/generated/java"
    filter(ReplaceTokens, tokens: [
            'NAME'   : "${project.name}".toString(),
            'VERSION': "${project.version}".toString()
    ])

}
compileJava.dependsOn generateSources


dependencies {
    api 'org.dmfs:jems:' + JEMS_VERSION
    api 'org.dmfs:rfc3986-uri:0.8.1'
    api 'org.dmfs:rfc5545-datetime:0.3'
    api 'org.dmfs:http-client-essentials:' + HTTP_CLIENT_ESSENTIALS_VERSION

    implementation 'net.iharder:base64:2.3.9'
    implementation 'org.dmfs:http-client-headers:' + HTTP_CLIENT_ESSENTIALS_VERSION
    implementation 'org.dmfs:http-client-types:' + HTTP_CLIENT_ESSENTIALS_VERSION
    implementation 'org.dmfs:http-client-basics:' + HTTP_CLIENT_ESSENTIALS_VERSION
    implementation 'org.dmfs:http-executor-decorators:' + HTTP_CLIENT_ESSENTIALS_VERSION
    implementation 'org.json:json:20220924'

    testImplementation 'org.dmfs:jems-testing:' + JEMS_VERSION
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.mockito:mockito-core:4.8.0'
    testImplementation 'org.hamcrest:hamcrest-library:2.2'
    testImplementation 'org.dmfs:http-client-mockutils:' + HTTP_CLIENT_ESSENTIALS_VERSION
    testImplementation 'org.jmockit:jmockit:1.49'
}
